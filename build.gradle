group 'org.simonscode'
version '3.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.telegram', name: 'telegrambots', version: '3.6'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'org.simonscode.telegrambots.framework.Controller'
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Telegram Bots Framework'
                packaging 'jar'
                description 'An application designed to make java bot development quicker, share server ressources.'
                url 'https://github.com/AnyTimeTraveler/telegrambots-framework'

                scm {
                    url 'https://github.com/AnyTimeTraveler/telegrambots-framework'
                    connection 'scm:https://github.com:AnyTimeTraveler/telegrambots-framework.git'
                    developerConnection 'scm:git://github.com:AnyTimeTraveler/telegrambots-framework.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/AnyTimeTraveler/telegrambots-framework/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'anytimetraveler'
                        name 'Simon Struck'
                        email 'simonstruck70@gmail.com'
                    }
                }
            }
        }
    }
}